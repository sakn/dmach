plugins {
    id "com.android.application"
    id "dagger.hilt.android.plugin"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlinx-serialization"
    id "com.github.triplet.play"
}

kapt {
    correctErrorTypes true
    useBuildCache true
}

hilt {
    enableAggregatingTask true
}

play {
    track.set("internal")
    resolutionStrategy.set(com.github.triplet.gradle.androidpublisher.ResolutionStrategy.IGNORE)
    defaultToAppBundles.set(true)
    serviceAccountCredentials.set(rootProject.file("signing/play-account.json"))
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}

android {
    namespace "net.simno.dmach"
    compileSdk versions.compileSdk
    ndkVersion versions.ndk

    defaultConfig {
        applicationId "net.simno.dmach"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 30005
        versionName "3.2"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        vectorDrawables {
            useSupportLibrary true
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile rootProject.file("signing/app-upload.jks")
            storePassword propOrDef("STORE_PASSWORD", "")
            keyAlias propOrDef("KEY_ALIAS", "")
            keyPassword propOrDef("KEY_PASSWORD", "")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.0-alpha02"
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
        jniLibs {
            useLegacyPackaging true
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        androidTest.assets.srcDirs += files("$projectDir/databases".toString())
    }
    lintOptions {
        checkReleaseBuilds false
        ignoreTestSources true
        abortOnError true
        checkDependencies true
        disable "GradleDependency"
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"

    implementation platform("androidx.compose:compose-bom:2022.12.00")
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    implementation "androidx.activity:activity-compose:1.6.1"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha04"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"

    implementation "androidx.paging:paging-common-ktx:$versions.paging"
    implementation "androidx.paging:paging-runtime-ktx:$versions.paging"
    implementation "androidx.paging:paging-compose:1.0.0-alpha17"

    implementation "androidx.room:room-common:$versions.room"
    implementation "androidx.room:room-runtime:$versions.room"
    implementation "androidx.room:room-ktx:$versions.room"
    implementation "androidx.room:room-paging:$versions.room"
    kapt "androidx.room:room-compiler:$versions.room"

    implementation "com.google.dagger:hilt-android:$versions.hilt"
    kapt "com.google.dagger:hilt-compiler:$versions.hilt"

    implementation "com.github.simonnorberg:dmach-externals:1.13.0"
    implementation "net.simno.kortholt:kortholt:1.12.0"

    implementation "com.squareup.leakcanary:plumber-android:2.10"

    androidTestImplementation "androidx.room:room-testing:$versions.room"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    androidTestImplementation "androidx.test.ext:junit:$versions.junit"
    androidTestImplementation "androidx.test.ext:truth:$versions.truth"

    testImplementation "androidx.paging:paging-common-ktx:$versions.paging"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "androidx.test:runner:1.5.1"
    testImplementation "androidx.test.ext:junit:$versions.junit"
    testImplementation "androidx.test.ext:truth:$versions.truth"
    testImplementation "org.mockito:mockito-core:4.9.0"
    testImplementation "org.robolectric:robolectric:4.9"
}
