buildscript {
    ext.versions = [
            'kotlin'    : '1.6.21',
            'coroutines': '1.6.2',
            'compileSdk': 32,
            'minSdk'    : 28,
            'targetSdk' : 32,
            'ndk'       : '24.0.8215888',
            'compose'   : '1.2.0-beta03',
            'lifecycle' : '2.5.0-rc01',
            'paging'    : '3.2.0-alpha01',
            'room'      : '2.5.0-alpha02',
            'hilt'      : '2.42',
            'junit'     : '1.1.3',
            'truth'     : '1.4.0'
    ]
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$versions.kotlin"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.hilt"
        classpath 'com.github.triplet.gradle:play-publisher:3.7.0'
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.6.1'
    id 'com.github.ben-manes.versions' version '0.42.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://jitpack.io'
            content {
                includeModule('com.github.simonnorberg', 'dmach-externals')
            }
        }
    }
}

subprojects {
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { details ->
                def group = details.requested.group
                if (group.startsWith('androidx.compose') && !group.contains('material3')) {
                    details.useVersion versions.compose
                }
                if (group.startsWith('androidx.lifecycle')) {
                    details.useVersion versions.lifecycle
                }
            }
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '11'
            allWarningsAsErrors = true
            freeCompilerArgs += '-opt-in=kotlin.Experimental'
            freeCompilerArgs += '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-opt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-opt-in=androidx.compose.foundation.ExperimentalFoundationApi'
            freeCompilerArgs += '-opt-in=androidx.compose.material3.ExperimentalMaterial3Api'
            freeCompilerArgs += '-opt-in=androidx.compose.ui.ExperimentalComposeUiApi'
        }
    }
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint('0.45.2')
        }
    }
}
