import net.simno.dmach.Libs
import net.simno.dmach.Versions

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Kotlin.serializationPlugin
        classpath Libs.Google.ossLicensesPlugin
        classpath Libs.Google.Hilt.plugin
        classpath Libs.playPublisherPlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.10.1'
    id 'com.github.ben-manes.versions' version '0.36.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = Versions.java
            allWarningsAsErrors = true
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
        }
    }
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(Versions.ktlint)
        }
    }
}
